// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

ext {
    targetSdkVersion = 31
    compileSdkVersion = 31
    buildToolsVersion = '31.0.0'

    group = 'org.eclipse.paho'

    sampleArchivesBaseName = 'org.eclipse.paho.android.sample'
    sampleVersion = '1.1.1'

    serviceArchivesBaseName = 'org.eclipse.paho.android.service'
    serviceVersion = '1.2.0'

    clientVersion = "1.2.5"

    mavenUrl = "https://repo.eclipse.org/content/repositories/paho-releases/"

    material = "1.8.0"
    appcompat = "1.6.1"
    recyclerview = "1.3.0"
    localbroadcastmanager = "1.1.0"
}

def getSdkDirFromLocalProperties() {
    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
        properties.load(instr)
    }
    properties.getProperty('sdk.dir')
}


def androidHome = System.env['ANDROID_HOME']

def androidHomeIsDefined = androidHome?.trim()

def sdkDir = androidHomeIsDefined ? androidHome : getSdkDirFromLocalProperties()

println "SDK dir: $sdkDir"


allprojects {

    repositories {
        mavenCentral()
        google()
        maven {
            url "file://$sdkDir/extras/android/m2repository/"
        }
    }

    version = rootProject.ext.serviceVersion
    group = rootProject.ext.group
}
