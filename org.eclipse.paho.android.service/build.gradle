apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24

        testApplicationId "org.eclipse.paho.android.service.test"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        preDexLibraries = false
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    maven { url rootProject.ext.mavenUrl }
}

configurations {
    //androidTestCompile.exclude module: 'org.eclipse.paho.client.mqttv3'
}

dependencies {
    compile "org.eclipse.paho:org.eclipse.paho.client.mqttv3:${rootProject.ext.clientVersion}"
    compile "com.android.support:support-v4:${rootProject.ext.supportLibVersion}"
    compile fileTree(include: ['*.jar'], dir: 'libs')
}

android.libraryVariants.all { variant ->
    def name = name.substring(0, 1).toUpperCase() + name.substring(1)
    task("generate${name}Javadoc", type: Javadoc) {
        title = "$name $version API"
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar =
            "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        afterEvaluate {
            classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        }
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        options.addStringOption('Xdoclint:none', '-quiet')
        failOnError false
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}

def snapshot = project.version.endsWith('-SNAPSHOT')

// Generate Javadoc
task("generateJavadocJar", type: Jar) {
    dependsOn snapshot? 'generateDebugJavadoc': 'generateReleaseJavadoc'
    from("$buildDir/docs/javadoc")
    classifier = 'javadoc'
}

// Generate Sources Jar
task("generateSourcesJar", type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// Copy the Paho Java client into the libs directory to bundle it with the AAR
task("copyLibs", type: Copy) {
    doFirst {
        println "Copying MQTT Jar into libs directory"
    }
    from configurations.compile
    into 'libs'
    include 'org.eclipse.paho*'
}

// Generate Plain Jar Version of library (good for legacy users)
task("generateLibraryJar", type: Jar) {
    afterEvaluate {
        dependsOn compileReleaseJavaWithJavac
        from compileReleaseJavaWithJavac.outputs.files.collect()
    }
}

publishing {
    publications {
        paho(MavenPublication) {
            artifact generateJavadocJar
            artifact generateSourcesJar
            artifact generateLibraryJar
            for(def file in fileTree(dir: "$buildDir/outputs/aar", include: ['*-release.aar'])) {
                artifact file
            }
        }
    }
    repositories {
        if (project.hasProperty('eclipseRepoUsername')) {
            maven {
                name 'eclipse'
                url 'https://repo.eclipse.org/content/repositories/paho-' + (snapshot? 'snapshots/': 'releases/')
                credentials {
                    username eclipseRepoUsername
                    password eclipseRepoPassword
                }
            }
        } else {
            maven {
                url "$buildDir/repo"
            }
        }
    }
}

task debug doLast {
    configurations.compile.each { println it }
}
